; MCM Extender - Stentorious

; Check for requirements
if eval IsModLoaded "The Mod Configuration Menu.esp" == 0
	MessageBoxEx "MCM Extender missing requirement!%rInstall The Mod Configuration Menu."
	return
endif
if eval GetNVSEVersionFull < 6.45
	MessageBoxEx "MCM Extender missing requirement!%rInstall xNVSE 6.4.5+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 360
	MessageBoxEx "MCM Extender missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 181
	MessageBoxEx "MCM Extender missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "MCM Extender missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif

; MCM Extender version
float MCMExtenderVersion = 1.52
SetUIFloatAlt "HUDMainMenu\_MCMExt+Version" MCMExtenderVersion
SetUIStringAlt "HUDMainMenu\_MCMExt+VersionStr" "MCM Extender v1.5.2"

; Cache MCM forms
Goo1.AuxVarSetRef "*MCMExt_Refs" (GFFM "The Mod Configuration Menu.esp" "ADE") 0 ; MCM
;Goo1.AuxVarSetRef "*MCMExt_Refs" (GFFM "The Mod Configuration Menu.esp" "AEA") 1 ; MCMModList
;Goo1.AuxVarSetRef "*MCMExt_Refs" (GFFM "The Mod Configuration Menu.esp" "B13") 2 ; MCMNavigate
;Goo1.AuxVarSetRef "*MCMExt_Refs" (GFFM "The Mod Configuration Menu.esp" "B15") 3 ; MCMScale
;Goo1.AuxVarSetRef "*MCMExt_Refs" (GFFM "The Mod Configuration Menu.esp" "B18") 4 ; MCMList
;Goo1.AuxVarSetRef "*MCMExt_Refs" (GFFM "The Mod Configuration Menu.esp" "AE6") 5 ; MCMMods

int iSize
array_var aFiles
array_var aJSON
array_var aModID
string_var sFilePath

; Validate menu JSONs

aFiles = GetFilesInFolder "MCM" "*.json"
if eval (iSize = Ar_Size aFiles) > 0
	PrintC "MCM Extender: Loading JSON files..."
	aFiles = Ar_Sort aFiles 1
	aModID = ar_Construct "array"
	while (iSize -= 1) > -1
		sFilePath = sv_Construct "Data\MCM\%z" (aFiles[iSize])
		; Check valid JSON
		if eval TestExpr (aJSON = ReadFromJSON (sFilePath))
		else
			PrintC "MCM Extender: Invalid -> %z" sFilePath
			continue
		endif
		; Check MCM Extender version
		if eval ar_HasKey aJSON "minMCMVersion" == 0 || (aJSON["minMCMVersion"]) > MCMExtenderVersion
			PrintC "MCM Extender: Requires MCM Extender %.2f+ -> %z" (aJSON["minMCMVersion"]) sFilePath
			continue
		endif
		; Check requirements array
		if eval ar_HasKey aJSON "requirements" && (Call (CompileScript "MCMExtender\CheckRequirements.gek") aJSON) == 0
			PrintC "MCM Extender: Missing requirements -> %z" sFilePath
			continue
		endif
		; Check duplicate IDs
		if eval (Ar_Find (aJSON["modName"]) aModID) != Ar_BadNumericIndex
			PrintC "MCM Extender: Duplicate modName -> %z" sFilePath
			continue
		endif
		;PrintC "MCM Extender: Loading... -> %z" sFilePath
		ar_Append aModID (aJSON["modName"])
		Goo1.AuxVarSetRef "*MCMExt_Clones" BobbyPin -1
		Goo1.AuxVarSetStr "*MCMExt_JSON" (sFilePath) -1
		; Translate name
		sFilePath = aJSON["displayName"]
		if eval sFilePath[0] == "$"
			sFilePath = GetINIString_Cached ("Translations:" + (sFilePath)) ("..\MCM\Translations\" + (aJSON["modName"]) + ".ini") (sFilePath)
			CloseFileSO ("..\MCM\Translations\" + (aJSON["modName"]) + ".ini") 0
		endif
		Goo1.AuxVarSetStr "*MCMExt_ModName" (sFilePath) -1
	loop
	if eval (Ar_Size aModID) > 0
		Goo1.AuxVarSetFromArr "*MCMExt_ModID" aModID
	endif

	PrintC "MCM Extender: Loaded %g mod menus." (Ar_Size aModID)
endif

aFiles = aJSON = aModID = Ar_Null
sv_Destruct sFilePath

; Init aux
; State
Goo1.AuxVarSetFlt "*MCMExt_State" 0 0		; Is JSON menu open
Goo1.AuxVarSetFlt "*MCMExt_State" 0 1		; MenuID
Goo1.AuxVarSetFlt "*MCMExt_State" 0 2		; Active mouseover
Goo1.AuxVarSetStr "*MCMExt_State" ("") 3	; JSON path
Goo1.AuxVarSetStr "*MCMExt_State" ("") 4	; modName
Goo1.AuxVarSetStr "*MCMExt_State" ("") 5	; saveFile
Goo1.AuxVarSetFlt "*MCMExt_State" 0 6		; Mod index
Goo1.AuxVarSetFlt "*MCMExt_State" 0 7		; Message box open

; Key
Goo1.AuxVarSetFlt "*MCMExt_Keys" 0 0
Goo1.AuxVarSetFlt "*MCMExt_Keys" 0 1

; Get screen dimensions
Goo1.AuxVarSetFlt "*_ScreenWidth" (GetNumericINISetting "iSize W:Display")
Goo1.AuxVarSetFlt "*_ScreenHeight" (GetNumericINISetting "iSize H:Display")

; Menu events
SetOnMenuOpenEventHandler (CompileScript "MCMExtender\OnPauseMenuOpen.gek") 1 1013
SetOnMenuCloseEventHandler (CompileScript "MCMExtender\OnPauseMenuClose.gek") 1 1013

; Default settings confirmation
Goo1.AuxVarSetFlt "*MCMExt_Dev" (eval GetINIFloat_Cached "General:bDefaultSettingsConfirmation" "Stentorious\MCMExtender.ini" 1) 0
SetUIFloatAlt "HUDMainMenu\_MCMExt+HighlightModified" (eval GetINIFloat_Cached "General:bHighlightModifiedSettings" "Stentorious\MCMExtender.ini" 0)
SetOnMenuClickEventHandler (CompileScript "MCMExtender\OnMenuClick.gek") 1 "StartMenu#609"
SetEventHandler "OnButtonDown:32768" (CompileScript "MCMExtender\OnKeyDown.gek")
CloseFileSO "Stentorious\MCMExtender.ini" 0

; Keyboard events
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 264			; Wheel Up
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 265			; Wheel Down
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 256			; Left Click

; Search bar events
SetOnMenuClickEventHandler (CompileScript "MCMExtender\OnMenuClick.gek") 1 "StartMenu#606"
SetOnMenuClickEventHandler (CompileScript "MCMExtender\OnMenuClick.gek") 1 "StartMenu#607"
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 1			; Esc
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 15			; Tab
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 33			; F
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 46			; C
SetOnKeyDownEventHandler (CompileScript "MCMExtender\OnKeyDown.gek") 1 47			; V
array_var aKeys = Ar_List 2 3 4 5 6 7 8 9 10 11 16 17 18 19 20 21 22 23 24 25
array_var aTemp = Ar_List 30 31 32 33 34 35 36 37 38 44 45 46 47 48 49 50 14 28 57
ar_Cat aKeys aTemp
aTemp = Ar_List 199 207 203 205
ar_Cat aKeys aTemp
if eval (int iSize = ar_Size aKeys) > 0
	while (iSize -= 1) > -1
		SetOnKeyDownEventHandler (CompileScript "MCMExtender\MenuSearchInput.gek") 1 (aKeys[iSize])
		SetOnKeyUpEventHandler (CompileScript "MCMExtender\OnKeyUp.gek") 1 (aKeys[iSize])
	loop
endif
aKeys = aTemp = ar_Null

; Decrease MCM quest delay (High refresh rate fix)
SetQuestDelay (Goo1.AuxVarGetRef "*MCMExt_Refs" 0) 0.001
SetQuestDelay MCMList 0.001
SetQuestDelay MCMModList 0.001
SetQuestDelay MCMNavigate 0.001
SetQuestDelay MCMScale 0.001

; Controller scroll events
SetEventHandler "OnButtonDown:15" (CompileScript "MCMExtender\OnButtonDown.gek")
SetEventHandler "OnButtonUp:15" (begin function {int iKeyCode}
	if eval Goo1.AuxVarType "*MCMExt_QBut" == 0
		return
	endif
	Goo1.AuxTimerStop "*MCMExt_QBut"
	Goo1.AuxVarErase "*MCMExt_QBut"
end)
