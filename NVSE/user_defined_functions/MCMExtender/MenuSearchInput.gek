int iKeyID

int iCursorPos
int iLength
int iSearch
float fWidth
float fHeight
string_var sSearch
string_var sCursor
string_var sCharacter

begin Function {iKeyID}

	if eval MenuMode 3 || GetActiveMenuMode != 1013 || GetUIFloatAlt "StartMenu\_MCMExt+Search" != 1
		return
	endif

	sSearch = GetUIString "StartMenu\MCM\MCM_Search\_search_text_1"
	iCursorPos = Goo1.AuxVarGetFlt "*MCMExt_Cursor"

	; Update cursor position
	if eval iKeyID == 203 || iKeyID == 205 || iKeyID == 199 || iKeyID == 207
		iLength = sv_Length sSearch

		; Left arrow
		if eval iKeyID == 203
			iCursorPos -= 1
		; Right arrow
		elseif eval iKeyID == 205
			iCursorPos += 1
		; Home
		elseif eval iKeyID == 199
			iCursorPos = 0
		; End
		elseif eval iKeyID == 207
			iCursorPos = iLength
		endif
		iCursorPos = Clamp iCursorPos 0 iLength

	; Enter
	elseif eval iKeyID == 28
		SetUIFloatAlt "StartMenu\_MCMExt+Search" 2
		sv_Destruct sSearch
		PlaySound UIHackingCharEnter 1
		return

	; Ctrl-F
	elseif eval iKeyID == 33 && (IsKeyPressed 29 2 || IsKeyPressed 157 2)
		sv_Destruct sSearch
		return

	; Backspace
	elseif eval iKeyID == 14
		if eval iCursorPos > 0
			iCursorPos -= 1
			sSearch[iCursorPos] = ""
			iSearch = 1
			PlaySound UIHackingCharSingle 1
		endif

	; Delete
	elseif eval iKeyID == 211
		if eval iCursorPos < (iLength = sv_Length sSearch)
			sSearch[iCursorPos] = ""
			iSearch = 1
			PlaySound UIHackingCharSingle 1
		endif

	; Characters
	else
		GetStringUIDimensions (sCharacter = ToANSIChar iKeyID 0) (GetUIFloatAlt "StartMenu\_MCMExt+FontGeneral") 100 (fWidth) (fHeight) (iLength)
		if eval fWidth <= 2
			sCharacter = ToANSIChar iKeyID 1
		endif
		sv_Insert "%z" sCharacter sSearch iCursorPos
		iCursorPos += 1
		iSearch = 2
		PlaySound UIHackingCharSingle 1
	endif

	; Update string
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_text_1" "%z" sSearch
	SetUIFloatAlt "StartMenu\_MCMExt+Search" 0
	SetUIFloatAlt "StartMenu\_MCMExt+Search" 1

	; Update cursor
	Goo1.AuxVarSetFlt "*MCMExt_Cursor" iCursorPos
	sCursor = sSearch
	sv_Insert "|" sCursor iCursorPos
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_cursor_1" "%z" sCursor
	sCursor[iCursorPos] = " "
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_cursor_2" "%z" sCursor
	SetUIFloatGradual "StartMenu\_MCMExt+Pulse" 1
	SetUIFloatGradual "StartMenu\_MCMExt+Pulse" 1 2.5 1 2

	; Queue input
	if eval Goo1.AuxVarType "*MCMExt_QKey" == 0 || Goo1.AuxVarGetFlt "*MCMExt_QKey" != iKeyID
		Goo1.AuxTimerStart "*MCMExt_QKey" 0.5 13
		Goo1.AuxVarSetFlt "*MCMExt_KeyStack" iKeyID -1
	else
		Goo1.AuxTimerStart "*MCMExt_QKey" 0.05 13
	endif
	Goo1.AuxVarSetFlt "*MCMExt_QKey" iKeyID

	; Search
	if eval iSearch == 1 || (iSearch == 2 && GetUIFloatAlt "StartMenu\MCM\MCM_Search\_search_count" > 0)
		Call (CompileScript "MCMExtender\MenuSearch.gek") (sSearch)
	endif

	sv_Destruct sSearch sCursor sCharacter

end