int iMenuID

int iSize
ref rMenuItem
array_var aTemp

Begin Function {iMenuID}

	if eval GetActiveMenuMode == 1013 || GetUIFloatAlt "StartMenu\MCM\_Menu" == 1
		return
	endif

	; Stop menu loop
	SGMLC (CompileScript "MCMExtender\MenuLoop.gek") 0

	; Erase translation map
	AuxStringMapDestroy "*MCMExt_Translations"

	; Alert mods that values have changed
	if eval Goo1.AuxVarSize "*MCMExt_Updated" > 0
		aTemp = Goo1.AuxVarGetAsArr "*MCMExt_Updated"
		DispatchEventAlt "MCMExtUpdate" (Ar_Unique aTemp)
		Goo1.AuxVarErase "*MCMExt_Updated"
		;PrintC "MCM Extender: Updated mods -> %z" (Ar_Unique aTemp)
	endif

	; Saved cached INI's if menu exit skipped
	if eval Goo1.AuxVarSize "*MCMExt_INICache" > 0
		aTemp = Goo1.AuxVarGetAsArr "*MCMExt_INICache"
		aTemp = Ar_Unique aTemp
		iSize = Ar_Size aTemp
		while (iSize -= 1) > -1
			SaveCachedIni (aTemp[iSize])
			CloseFileSO (aTemp[iSize]) 0
			;PrintC "MCM Extender: Saved INI -> %z" (aTemp[iSize])
		loop
		Goo1.AuxVarErase "*MCMExt_INICache"
	endif

	; Clear JSON cache
	if eval Goo1.AuxVarSize "*MCMExt_JSONCache" > 0
		aTemp = Goo1.AuxVarGetAsArr "*MCMExt_JSONCache"
		aTemp = Ar_Unique aTemp
		iSize = Ar_Size aTemp
		while (iSize -= 1) > -1
			CloseFileSO (aTemp[iSize]) 1
			;PrintC "MCM Extender: Clear JSON cache -> %z" (aTemp[iSize])
		loop
		Goo1.AuxVarErase "*MCMExt_JSONCache"
	endif

	; Remove clones from MCM FormList
	iSize = ListGetCount MCMMods
	while (iSize -= 1) > -1
		rMenuItem = ListGetNthForm MCMMods iSize
		if eval (GetSourceModIndex rMenuItem) == 255
			ListRemoveForm MCMMods rMenuItem
			;PrintC "MCM Extender: Remove clone -> %z" (LNGetName rMenuItem)
		endif
	loop

	; Reset MCM FormList from search
	if eval Goo1.AuxVarGetFlt "*MCMExt_Search"
		Goo1.AuxVarSetFlt "*MCMExt_Search" 0
		Goo1.AuxVarSetFlt "*MCMExt_Cursor" 0
		aTemp = Goo1.AuxVarGetAsArr "*MCMListCache"
		ListAddArray MCMMods aTemp
		EK 18
	endif

	; MCM Bug Fix 2
	StopQuest MCMScale
	StopQuest MCMList

	aTemp = ar_Null

End