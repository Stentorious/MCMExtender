int iSubmenu
int iOption

int iSize
int iValue
array_var aJSON
array_var aOption
string_var sText
string_var sTranslate

Begin Function {}

	SetUIFloat "StartMenu/MCM/_ShowList" 2

	; Exit search on select key option
	if eval GetUIFloatAlt "StartMenu\_MCMExt+Search" == 1
		SetUIFloatAlt "StartMenu\_MCMExt+Search" 2
	endif

	iSubmenu = GetUIFloat "StartMenu/MCM/_ActiveSubMenu"
	iOption = GetUIFloat "StartMenu/MCM/_ActiveOption"
	aJSON = ReadFromJSON (Goo1.AuxVarGetStr "*MCMExt_State" 3) "" 0 1
	aOption = aJSON["submenus"][$iSubmenu]["options"][$iOption]

	; Translation
	if eval (TypeOf aOption["title"]) == "StringMap"
		sText = Call (CompileScript "MCMExtender\GetString.gek") (aOption["title"])
	else
		sText = aOption["title"]
	endif
	if eval sText[0] == "$" && (sTranslate = AuxStringMapGetStr "*MCMExt_Translations" (sText)) != ""
		sText = sTranslate
	endif
	SetUIString "StartMenu/MCM/*:3/_title" (sText)

	iSize = Ar_Size aOption["strings"]
	while (iSize -= 1) > -1

		; Translation
		if eval (TypeOf aOption["strings"][iSize]) == "StringMap"
			sText = Call (CompileScript "MCMExtender\GetString.gek") (aOption["strings"][iSize])
		else
			sText = aOption["strings"][iSize]
		endif
		if eval sText[0] == "$" && (sTranslate = AuxStringMapGetStr "*MCMExt_Translations" (sText)) != ""
			sText = sTranslate
		endif
		SetUIString ("StartMenu/MCM/*:3/*:" + $(iSize + 1) + "/text/string") (sText)

		SetUIFloat ("StartMenu/MCM/*:3/*:" + $(iSize + 1) + "/_enable") 1
	loop

	; Update value
	iValue = Call (CompileScript "MCMExtender\GetValue.gek") (aOption["vars"][0])
	if eval GetUIFloatAlt "StartMenu/MCM/_AltInput"
		iValue += 1
		CallAfterFrames 2 ({int iTemp} => SetMCMFloat 3 0 "_selected" (iTemp - 1)) 1 iValue
	endif
	SetUIFloat "StartMenu/MCM/_Value" iValue

	aJSON = aOption = Ar_Null
	sv_Destruct sText sTranslate

End