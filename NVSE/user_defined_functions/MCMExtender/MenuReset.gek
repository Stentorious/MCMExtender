int iSize
int iOption
int iDefaultSystemcolor
float fTemp
float fType
array_var aJSON
array_var aForE
array_var aForE2
array_var aTemp
array_var aVariables
array_var aElementNumeric
array_var aElementString
string_var sSubmenu
string_var sType
string_var sText
string_var sTemp
string_var sTranslate
string_var sConfigPath

Begin Function {}

	; ===== RESET =====
	SetUIFloat "StartMenu/MCM/_Reset" 0

	aJSON = ReadFromJSON (Goo1.AuxVarGetStr "*MCMExt_State" 3) "" 0 1

	; Prevent script error when scrolling past final submenu at the bottom of the list
	sSubmenu = $(GetUIFloat "StartMenu/MCM/_ActiveSubMenu")
	if eval ar_HasKey aJSON["submenus"] (sSubmenu) == 0
		MCMNavigate.iTask = 2
		aJSON = Ar_Null
		sv_Destruct sSubmenu
		return
	endif

	SetUIFloat "StartMenu/MCM/*:1/_columns" (aJSON["submenus"][(sSubmenu)]["columns"])

	sConfigPath = Goo1.AuxVarGetStr "*MCMExt_State" 5

	; Hide images
	SetMCMFloatMass 1 1 36 "MCMExtImage/visible" 0

	; Reset brightness
	SetMCMFloatMass 1 1 36 "_brightnessMul" 1

	; Systemcolor
	iDefaultSystemcolor = GetUIFloatAlt "StartMenu/_MCMExt+Systemcolor"
	SetMCMFloatMass 1 1 36 "_MCMExt+Systemcolor" iDefaultSystemcolor

	; Element types
	aElementNumeric = Ar_List "type", "enable", "refresh", "indent", "brightnessMul"
	aElementString = Ar_List "title", "prefix", "suffix", "string", "textOff", "textOn"

	; Reset options
	foreach aForE <- (aJSON["submenus"][(sSubmenu)]["options"])
		aTemp = (aForE["value"])
		iOption = #aForE["key"]

		; Image display
		if eval Ar_HasKey aTemp "filename" || Ar_HasKey aTemp "image"
			sTemp = "StartMenu/MCM/*:1/*:" + (aForE["key"])

			; Legacy
			if eval Ar_HasKey aTemp "filename"
				SetUIString ((sTemp) + "/MCMExtImage/filename") (aTemp["filename"])
				SetUIFloat ((sTemp) + "/MCMExtImage/width") (aTemp["width"])
				SetUIFloat ((sTemp) + "/MCMExtImage/height") (aTemp["height"])
				SetUIFloat ((sTemp) + "/MCMExtImage/x") ((GetUIFloat "StartMenu/MCM/MCM_Options/x") + ((GetUIFloat "StartMenu/MCM/MCM_Options/width") / 2) - ((aTemp["width"]) / 2))
				SetUIFloat ((sTemp) + "/MCMExtImage/y") ((GetUIFloat "StartMenu/MCM/y") + ((GetUIFloat "StartMenu/MCM/height") / 2) - ((aTemp["height"]) / 2))
				if eval (fTemp = aTemp["systemcolor"]) == 1 || fTemp == 5
					fTemp = iDefaultSystemcolor
				endif
				SetUIFloat ((sTemp) + "/MCMExtImage/systemcolor") fTemp
			; 1.3.0+
			else
				SetUIString ((sTemp) + "/MCMExtImage/filename") (aTemp["image"]["filename"])
				SetUIFloat ((sTemp) + "/MCMExtImage/width") (aTemp["image"]["width"])
				SetUIFloat ((sTemp) + "/MCMExtImage/height") (aTemp["image"]["height"])
				fTemp = (GetUIFloat "StartMenu/MCM/MCM_Options/x") + ((GetUIFloat "StartMenu/MCM/MCM_Options/width") / 2) - ((aTemp["image"]["width"]) / 2)
				if eval Ar_HasKey aTemp["image"] "offsetX"
					fTemp += aTemp["image"]["offsetX"]
				endif
				SetUIFloat ((sTemp) + "/MCMExtImage/x") fTemp
				fTemp = (GetUIFloat "StartMenu/MCM/y") + ((GetUIFloat "StartMenu/MCM/height") / 2) - ((aTemp["image"]["height"]) / 2)
				if eval Ar_HasKey aTemp["image"] "offsetY"
					fTemp += aTemp["image"]["offsetY"]
				endif
				SetUIFloat ((sTemp) + "/MCMExtImage/y") fTemp
				if eval (fTemp = aTemp["image"]["systemcolor"]) == 1 || fTemp == 5
					fTemp = iDefaultSystemcolor
				endif
				SetUIFloat ((sTemp) + "/MCMExtImage/systemcolor") fTemp
			endif

			SetUIString ((sTemp) + "/_title") " "
			SetUIFloat ((sTemp) + "/_enable") 1
			SetUIFloat ((sTemp) + "/_type") 0
			SetUIFloat ((sTemp) + "/_highlight") 0
			SetUIFloat ((sTemp) + "/MCMExtImage/visible") 1
			continue
		endif

		foreach aForE2 <- aTemp
			sType = (TypeOf aForE2["value"])

			; Numeric
			if eval (Ar_Find aForE2["key"] aElementNumeric) != Ar_BadNumericIndex
				if eval sType == "StringMap"
					fTemp = Call (CompileScript "MCMExtender\GetValue.gek") (aForE2["value"])
				else
					fTemp = aForE2["value"]
				endif
				SetMCMFloat 1 iOption ("_" + (aForE2["key"])) (fTemp)
				continue

			; String
			elseif eval (Ar_Find aForE2["key"] aElementString) == Ar_BadNumericIndex
				continue
			endif

			; Translation
			if eval sType == "StringMap"
				sTemp = Call (CompileScript "MCMExtender\GetString.gek") (aForE2["value"])
				aForE2["value"] = sTemp
			else
				sTemp = aForE2["value"]
			endif
			if eval sTemp[0] == "$" && (sTranslate = AuxStringMapGetStr "*MCMExt_Translations" (sTemp)) != ""
				aForE2["value"] = sTranslate
			endif

			if eval (aForE2["key"]) == "mouse"
				continue
			elseif eval (aForE2["key"]) == "suffix"
				sTemp = "StartMenu/MCM/*:1/*:" + (aForE["key"]) + "/_suffix"
				SetUIFloat (sTemp) 1
				sTemp += "Text"
			elseif eval (aForE2["key"]) == "string"
				sTemp = "StartMenu/MCM/*:1/*:" + (aForE["key"]) + "/value/*:1/string"
			else
				sTemp = "StartMenu/MCM/*:1/*:" + (aForE["key"]) + "/_" + (aForE2["key"])
			endif

			SetUIString (sTemp) (aForE2["value"])

		loop

		; Option state switching
		if eval Ar_HasKey aTemp "state"
			if eval (Call (CompileScript "MCMExtender\GetValue.gek") aTemp["state"]) == aTemp["state"]["value"]
				fTemp = 1
			else
				fTemp = 0
				if eval Ar_HasKey aTemp["state"] "disable"
					fTemp += (aTemp["state"]["disable"] * 2)
				endif
			endif
			SetUIFloat ("StartMenu/MCM/*:1/*:" + (aForE["key"]) + "/_enable") fTemp
		endif

		; Process var object array
		if TestExpr aVariables = aTemp["vars"]
			sTemp = "StartMenu/MCM/*:1/*:" + (aForE["key"]) + "/value/*:1/string"
			iSize = Ar_Size aVariables

			; Multi value string
			if eval (fType = aTemp["type"]) == 8
				sText = ""
				while (iSize -= 1) > -1
					fTemp = Call (CompileScript "MCMExtender\GetValue.gek") aVariables[iSize]
					; Modified setting highlighting
					if eval Goo1.AuxVarGetFlt "*MCMExt_Dev" 1
						if eval ar_HasKey aVariables[iSize] "default" && fTemp != (aVariables[iSize]["default"])
							SetMCMFloat 1 iOption "_MCMExt+Systemcolor" 2
						endif
					endif
					sText = sv_Construct "%g, %z" fTemp sText
				loop
				SetUIString (sTemp) (sText[0:-3])
				continue

			; Color box
			elseif eval fType == 9
				while (iSize -= 1) > -1
					fTemp = Call (CompileScript "MCMExtender\GetValue.gek") aVariables[iSize]
					SetUIFloat ("StartMenu/MCM/*:1/*:" + (aForE["key"]) + "/_value" + $(iSize + 1)) fTemp
					; Modified setting highlighting
					if eval Goo1.AuxVarGetFlt "*MCMExt_Dev" 1
						if eval ar_HasKey aVariables[iSize] "default" && fTemp != (aVariables[iSize]["default"])
							SetMCMFloat 1 iOption "_MCMExt+Systemcolor" 2
						endif
					endif
				loop

			; Basic
			else
				fTemp = Call (CompileScript "MCMExtender\GetValue.gek") aVariables[0]

				; Modified setting highlighting
				if eval Goo1.AuxVarGetFlt "*MCMExt_Dev" 1
					if eval ar_HasKey aVariables[0] "default" && fTemp != (aVariables[0]["default"])
						SetMCMFloat 1 iOption "_MCMExt+Systemcolor" 2
					endif
				endif

				; Numeric
				if eval (ar_Find fType (ar_List 2, 4, 5, 6)) > -1
					SetMCMFloat 1 iOption "_value", fTemp
					continue

				; String
				elseif eval fType == 1
					; Translation
					fTemp = Clamp fTemp 0 ftemp
					if eval ar_HasKey aTemp "stringsAlt"
						sType = "stringsAlt"
					else
						sType = "strings"
					endif
					if eval (TypeOf aTemp[(sType)][fTemp]) == "StringMap"
						sText = Call (CompileScript "MCMExtender\GetString.gek") (aTemp[(sType)][fTemp])
					else
						sText = aTemp[(sType)][fTemp]
					endif
					if eval sText[0] == "$" && (sTranslate = AuxStringMapGetStr "*MCMExt_Translations" (sText)) != ""
						sText = sTranslate
					endif
					SetUIString (sTemp) (sText)
					continue

				; Float
				elseif eval fType == 2.5
					SetUIString (sTemp) (sv_Construct ("%." + $(aTemp["scale"]["valueDecimal"]) + "f") fTemp)
					continue

				; Scan code
				elseif eval fType == 3
					SetUIString (sTemp) (GetKeyName fTemp)
					continue
				endif
			endif

		endif
	loop

	aJSON = aForE = aForE2 = aTemp = aVariables = aElementNumeric = aElementString = Ar_Null
	sv_Destruct sType sText sTemp sTranslate sConfigPath sSubmenu

End