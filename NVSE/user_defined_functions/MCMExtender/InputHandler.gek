ref rOwner
string_var sTimerName

int iInput
int iScale
int iList
int iKey
ref rMCMQuest

begin Function {sTimerName, rOwner}

	iScale = GetUIFloatAlt "StartMenu/MCM/MCM_Scale/visible"
	iList = GetUIFloatAlt "StartMenu/MCM/MCM_List/visible"
	iKey = GetUIFloatAlt "StartMenu/MCM/MCM_PressControl/visible"

	if eval GetUIFloatAlt "StartMenu\MCM\_Menu" != 1 || iList || iKey || GetActiveMenuMode != 1013 || MenuMode 3
		sv_Destruct sTimerName
		return
	endif

	if eval sTimerName == "*MCMExt_QBut"
		rMCMQuest = Goo1.AuxVarGetRef "*MCMExt_Refs" 0
		iInput = Goo1.AuxVarGetFlt "*MCMExt_QBut"
		if eval iScale == 1
			PlaySound UIMenuPrevNext 1
			if eval iInput == 4
				MCMScale.iTask = 5
				if eval IsButtonPressed 4
					Goo1.AuxTimerStart "*MCMExt_QBut" 0.05 13
				endif
			elseif eval iInput == 8
				MCMScale.iTask = 6
				if eval IsButtonPressed 8
					Goo1.AuxTimerStart "*MCMExt_QBut" 0.05 13
				endif
			endif

		endif
		if eval iInput == 1
			PlaySound UIMenuFocus 1
			if eval GetVariable "iTargetOption" rMCMQuest
				MCMNavigate.iTask = 3
			else
				MCMNavigate.iTask = 1
			endif
			if eval IsButtonPressed 1
				Goo1.AuxTimerStart "*MCMExt_QBut" 0.075 13
			endif

		elseif eval iInput == 2
			PlaySound UIMenuFocus 1
			if eval GetVariable "iTargetOption" rMCMQuest
				MCMNavigate.iTask = 4
			else
				MCMNavigate.iTask = 2
			endif
			if eval IsButtonPressed 2
				Goo1.AuxTimerStart "*MCMExt_QBut" 0.075 13
			endif

		endif
	elseif eval sTimerName == "*MCMExt_QKey"
		Call (CompileScript "MCMExtender\MenuSearchInput.gek") (Goo1.AuxVarGetFlt "*MCMExt_QKey")
	endif

	sv_Destruct sTimerName

End