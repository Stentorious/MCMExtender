int iMouseOver
int iInitMenu
array_var aJSON
array_var aTemp
string_var sJSONPath
string_var sTemp
string_var sTranslate

Begin Function {}

	if eval GetActiveMenuMode != 1013 || GetUIFloatAlt "StartMenu/MCM/_Menu" == 0
		return
	endif

	; Is JSON menu selected
	if eval GetUIFloatAlt "StartMenu/MCM/_ActiveMod" != 255
		SetUIFloat "StartMenu/MCM/*:1/*:1/image/systemcolor" (GetUIFloat "StartMenu\_MCMExt+Systemcolor")
		SetUIFloat "StartMenu/MCM/*:1/*:1/VUI+Clone/systemcolor" (GetUIFloat "StartMenu\_MCMExt+Systemcolor")
		Goo1.AuxVarSetFlt "*MCMExt_State" 0 0
		SetUIFloat "StartMenu/_MCMExt+Menu" 0
		return
	elseif eval Goo1.AuxVarGetFlt "*MCMExt_State" 0 == 0
		Goo1.AuxVarSetFlt "*MCMExt_State" 1 0
		iInitMenu = 1
		SetUIFloat "StartMenu/_MCMExt+Menu" 1
	endif

	; Update active menu
	if eval (GetUIFloat "StartMenu/MCM/_ActiveMenu") != (Goo1.AuxVarGetFlt "*MCMExt_State" 1)
		Goo1.AuxVarSetFlt "*MCMExt_State" (GetUIFloat "StartMenu/MCM/_ActiveMenu") 1
		iInitMenu = 1
	endif

	; Init JSON Menu
	if eval iInitMenu == 1

		; Load JSON to memory
		sJSONPath = Goo1.AuxVarGetStr "*MCMExt_JSON" (Goo1.AuxVarGetFlt "*MCMExt_State" 1)

		aJSON = ReadFromJSON (sJSONPath) "" 0 1
		Goo1.AuxVarSetStr "*MCMExt_JSONCache" (sJSONPath) -1

		; Cache savefile
		Goo1.AuxVarSetStr "*MCMExt_State" (sJSONPath) 3
		Goo1.AuxVarSetStr "*MCMExt_State" (aJSON["modName"]) 4
		Goo1.AuxVarSetStr "*MCMExt_State" (aJSON["saveFile"]) 5

		; Update translations
		AuxStringMapDestroy "*MCMExt_Translations"
		aTemp = GetINISection "Translations" ("../MCM/Translations/" + (aJSON["modName"]) + ".ini")
		AuxStringMapSetFromArr "*MCMExt_Translations" aTemp

		; Build submenus
		if eval (Ar_Size (aJSON["submenus"])) > 1 && (GetUIFloat "StartMenu/MCM/*:5/SubMenu1/_enable") == 0
			Call (CompileScript "MCMExtender\RefreshSubmenus.gek") 0 0 0 0 aJSON
		endif

		;PrintC "MCM Enhanced: Init Menu -> %z" (aJSON["modName"])

	endif

	; ===== RESET =====
	; Updates the actual menu
	if eval (GetUIFloat "StartMenu/MCM/_Reset") == 1
		Call (CompileScript "MCMExtender\MenuReset.gek")
	endif

	; ===== DEFAULT =====
	; Stent: Runs when the "Defaults" button is pressed in the MCM menu
	if GetUIFloat "StartMenu/MCM/_Default"
		Call (CompileScript "MCMExtender\MenuDefault.gek")
	endif

	; ===== NEW VALUE =====
	; Stent: Runs after setting any option
	if GetUIFloat "StartMenu/MCM/_NewValue"
		Call (CompileScript "MCMExtender\MenuNew.gek")
	endif

	; ===== SHOW LIST =====
	; Stent: Runs a string list option is selected
	if eval (GetUIFloat "StartMenu/MCM/_ShowList") == 1
		Call (CompileScript "MCMExtender\MenuShowList.gek")
	endif

	; ===== SHOW SCALE =====
	; Stent: Runs a scale option is selected
	; Eg. Int, Float, RBG, Multi Value. Type: 2, 2.5, 8, 9
	if eval (GetUIFloat "StartMenu/MCM/_ShowScale") == 1
		Call (CompileScript "MCMExtender\MenuShowScale.gek")
	endif

	; ===== DEFAULT SCALE =====
	; Stent: Runs when the "Default" button is pressed in scale slider menu
	; Eg. Int, Float, RBG, Multi Value. Type: 2, 2.5, 8, 9
	if GetUIFloat "StartMenu/MCM/_DefaultScale"
		Call (CompileScript "MCMExtender\MenuDefaultScale.gek")
	endif

	; Sets option mouseover text
	if eval (iMouseOver = GetUIFloat "StartMenu/MCM/*:1/_optionID")
		SetUIFloat "StartMenu/MCM/MCM_Info/visible" 1
		if eval Goo1.AuxVarGetFlt "*MCMExt_State" 2 != iMouseOver
			Goo1.AuxVarSetFlt "*MCMExt_State" iMouseOver 2
			aJSON = ReadFromJSON (Goo1.AuxVarGetStr "*MCMExt_State" 3) "" 0 1
			if eval Ar_HasKey (aJSON["submenus"][$(GetUIFloat "StartMenu/MCM/_ActiveSubMenu")]["options"][$iMouseOver]) "mouse"
				sTemp = aJSON["submenus"][$(GetUIFloat "StartMenu/MCM/_ActiveSubMenu")]["options"][$iMouseOver]["mouse"]
				if eval sTemp[0] == "$" && (sTranslate = AuxStringMapGetStr "*MCMExt_Translations" (sTemp)) != ""
					sTemp = sTranslate
				endif
				SetUIStringEx "StartMenu/MCM/*:9/string" (sTemp)
			endif
		endif
	else
		Goo1.AuxVarSetFlt "*MCMExt_State" -1 2
		SetUIFloat "StartMenu/MCM/MCM_Info/visible" 0
	endif

	; HSV Color Picker Support
	if eval (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/visible") == 1 && (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_UserInput") == 1
		ref rQuest = EditorIDToFormID "MCMScale"
		SetVariable "fVTemp1" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Red") rQuest
		SetVariable "fVTemp2" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Green") rQuest
		SetVariable "fVTemp3" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Blue") rQuest
	endif

	aJSON = aTemp = Ar_Null
	sv_Destruct sTemp sTranslate

End