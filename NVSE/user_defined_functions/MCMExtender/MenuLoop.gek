int iModIndex
int iMenu
int iSubmenu
int iOption
int iMouseOver
int iTemp
int iScrollUpdate
int iJSONUpdate
int iStateUpdate
ref rTemp
array_var aJSON
array_var aTemp
string_var sJSONPath
string_var sTemp
string_var sTranslate

Begin Function {}

	if eval GetUIFloatAlt "StartMenu/MCM/_Menu" != 1
		if eval GetUIFloatAlt "StartMenu/_MCMExt+Open" == 1
			if eval GetUIFloatAlt "StartMenu\_MCMExt+Search" == 1
				SetUIFloatAlt "StartMenu\_MCMExt+Search" 2
			endif
			SetUIFloatAlt "StartMenu/_MCMExt+Open" 0
			DispatchEventAlt "MCMExtMenuState" "" (-1)
			EK 1
			EK 15
			EK 264
			EK 265
			EK (Goo1.AuxVarGetFlt "*MCMExt_Keys" 0)
		endif
		return
	elseif eval GetUIFloatAlt "StartMenu/_MCMExt+Open" == 0
		SetUIFloatAlt "StartMenu/_MCMExt+Open" 1
		DK 1
		DK 15
		DK 264
		DK 265
		iTemp = GetControl 26
		Goo1.AuxVarSetFlt "*MCMExt_Keys" iTemp 0
		DK iTemp
	endif

	; Duplicate MCMList fix
	aTemp = GetListForms MCMMods
	if eval (ar_Size aTemp) != (ar_Size (ar_Unique aTemp))
		aTemp = ar_Unique aTemp
		ListClear MCMMods
		ListAddArray MCMMods aTemp
		MCMModList.iModCount = ListGetCount MCMMods
		SetMCMFloat 5 0 "_count" (MCMModList.iModCount)
		aTemp = Ar_Null
	endif

	; String list controller selection fix
	if eval GetUIFloatAlt "StartMenu/MCM/_ShowList" == 3 && GetUIFloatAlt "StartMenu/MCM/_AltInput" == 1 && MCMList.iTarget < 1
		MCMList.iTarget = 1
		SetMCMFloat 0 0 "_TargetList" 1
	endif

	; Controller swap fix
	rTemp = Goo1.AuxVarGetRef "*MCMExt_Refs" 0
	if eval GetUIFloatAlt "StartMenu/MCM/_AltInput" != GetVariable "iAltInput" rTemp
		SetVariable "iAltInput" (GetUIFloatAlt "StartMenu/MCM/_AltInput") rTemp
	endif

	; Keybind selection
	if eval GetUIFloatAlt "StartMenu/MCM/MCM_PressControl/visible" == 1

		; Exit search
		if eval GetUIFloatAlt "StartMenu\_MCMExt+Search" == 1
			SetUIFloatAlt "StartMenu\_MCMExt+Search" 2
		endif

		if eval Goo1.AuxVarGetFlt "*MCMExt_State" 7 == 0

			; Show disabled key message box
			;if eval IsKeyDisabled (iTemp = GKP 0 2) && iTemp != 1 && iTemp != 15 && iTemp != Goo1.AuxVarGetFlt "*MCMExt_Keys" 0
			;	Goo1.AuxVarSetFlt "*MCMExt_Keys" iTemp 1
			;	Goo1.AuxVarSetFlt "*MCMExt_State" 1 7
			;	MCMInstalled = 0
			;	MessageBoxExAlt (CompileScript "MCMExtender\MessageBoxDisabledKey.gek") "^Mod Configuration Menu^%q%k%q key is currently disabled.%r%rWould you like to bind anyways?|No|Yes" (Goo1.AuxVarGetFlt "*MCMExt_Keys" 1)
			;endif

			; Select quick load
			if eval IsKeyPressed (Goo1.AuxVarGetFlt "*MCMExt_Keys" 0) 2
				SetMCMFloat 0 0 "MCM_PressControl/visible" 0
				SetMCMFloat 0 0 "MCM_BG/MCM_PressControl/BG/visible" 0
				SetMCMFloat 1 0 "_selected" 0
				SetMCMFloat 0 0 "_NewValue" 1
				SetMCMFloat 0 0 "_Value" (Goo1.AuxVarGetFlt "*MCMExt_Keys" 0)
			endif

		endif
	endif

	; Update active mod
	if eval (iModIndex = GetUIFloatAlt "StartMenu/MCM/_ActiveMod") != (Goo1.AuxVarGetFlt "*MCMExt_State" 6)
		Goo1.AuxVarSetFlt "*MCMExt_State" iModIndex 6
		iScrollUpdate = 1
	endif

	; Update active menu
	if eval (iMenu = GetUIFloatAlt "StartMenu/MCM/_ActiveMenu") != (Goo1.AuxVarGetFlt "*MCMExt_State" 1)
		Goo1.AuxVarSetFlt "*MCMExt_State" iMenu 1
		iJSONUpdate = 1
		iScrollUpdate = 1
	endif

	; Update options scrollbar
	SetUIFloatAlt "StartMenu/MCM/MCM_Options/_lines_visible" (GetVariable "iMaxLines" rTemp)
	SetUIFloatAlt "StartMenu/MCM/MCM_Options/_lines_total" (GetVariable "iLines" rTemp)

	; MCMSubShift fix
	if eval iScrollUpdate == 1 && MCMSubShift > 0
		iTemp = GetMaxOf 0 (GetMCMFloat 5 0 "_offset")
		MCMModSkip = iTemp
		SetMCMFloat 5 0 "_count" 0
		SetMCMFloat 5 0 "_offset" iTemp
		SetMCMFloat 0 0 "_ActiveOption" 0
		iTemp = GetMCMFloat 5 0 "_selected"
		SetMCMFloat 5 0 "_selected" iTemp
		MCMSubShift = 0
		MCMModList.iModIndex = 0
		MCMModList.iWidthCount2 = 0
		MCMModList.fSubWidth = 0
		MCMModsListed.RemoveAllItems
		SetMCMFloat 0 0 "_ActiveSubMenu" 0
		SetMCMFloatMass 5 24 33 "_enable" 0
		SetStage rTemp 1
		SetMCMFloat 0 0 "_TargetMenu" iTemp
		SetMCMFloat 0 0 "_TargetSubMenu" (GetVariable "iTargetSubMenu" rTemp)
		SetMCMFloat 0 0 "_TargetOption" (GetVariable "iTargetOption" rTemp)
		SetVariable "iTargetMenu" iTemp rTemp
		MCMNavigate.iTask = 0
		StopQuest MCMNavigate
	endif

	; Is JSON menu selected
	if eval iModIndex != 255
		; Reset JSON menu
		if eval Goo1.AuxVarGetFlt "*MCMExt_State" 0 == 1
			SetMCMFloatMass 1 1 36 "_MCMExt+Default" 1
			SetMCMFloatMass 1 1 36 "MCMExtImage/visible" 0
			SetMCMFloatMass 1 1 36 "_brightnessMul" 1
			SetMCMFloatMass 1 1 36 "_decimalColor" -1
			SetMCMFloatMass 1 1 36 "_systemcolor" (GetUIFloatAlt "StartMenu/_MCMExt+Systemcolor")
			Goo1.AuxVarSetFlt "*MCMExt_State" 0 0
			SetUIFloat "StartMenu/_MCMExt+Menu" 0
			DispatchEventAlt "MCMExtMenuState" "" 0
		endif
		return
	elseif eval Goo1.AuxVarGetFlt "*MCMExt_State" 0 == 0
		; Init JSON
		Goo1.AuxVarSetFlt "*MCMExt_State" 1 0
		iJSONUpdate = 1
		iStateUpdate = 1
		SetUIFloat "StartMenu/_MCMExt+Menu" 1
	endif

	; Init JSON Menu
	if eval iJSONUpdate == 1

		; Load JSON to memory
		sJSONPath = Goo1.AuxVarGetStr "*MCMExt_JSON" (Goo1.AuxVarGetFlt "*MCMExt_State" 1)

		aJSON = ReadFromJSON (sJSONPath) "" 0 1
		Goo1.AuxVarSetStr "*MCMExt_JSONCache" (sJSONPath) -1

		; Cache savefile
		Goo1.AuxVarSetStr "*MCMExt_State" (sJSONPath) 3
		Goo1.AuxVarSetStr "*MCMExt_State" (aJSON["modName"]) 4
		Goo1.AuxVarSetStr "*MCMExt_State" (aJSON["saveFile"]) 5

		; Update translations
		AuxStringMapDestroy "*MCMExt_Translations"
		aTemp = GetINISection "Translations" ("../MCM/Translations/" + (aJSON["modName"]) + ".ini")
		AuxStringMapSetFromArr "*MCMExt_Translations" aTemp

		; Build submenus
		if eval (Ar_Size (aJSON["submenus"])) > 1 && (GetUIFloat "StartMenu/MCM/*:5/SubMenu1/_enable") == 0
			Call (CompileScript "MCMExtender\RefreshSubmenus.gek") 0 0 0 0 aJSON
		endif

		;PrintC "MCM Enhanced: Init Menu -> %z" (aJSON["modName"])

	endif

	; ===== RESET =====
	; Updates the actual menu
	if eval (GetUIFloat "StartMenu/MCM/_Reset") == 1
		iStateUpdate = 1
		Call (CompileScript "MCMExtender\MenuReset.gek")
	endif

	; Dispatch menu switch event
	if eval iStateUpdate
		DispatchEventAlt "MCMExtMenuState" (Goo1.AuxVarGetStr "*MCMExt_State" 4) (GetUIFloat "StartMenu/MCM/_ActiveSubMenu")
	endif

	; ===== DEFAULT =====
	; Stent: Runs when the "Defaults" button is pressed in the MCM menu
	if GetUIFloat "StartMenu/MCM/_Default"
		Call (CompileScript "MCMExtender\MenuDefault.gek")
	endif

	; ===== NEW VALUE =====
	; Stent: Runs after setting any option
	if GetUIFloat "StartMenu/MCM/_NewValue"
		Call (CompileScript "MCMExtender\MenuNew.gek")
	endif

	; ===== SHOW LIST =====
	; Stent: Runs a string list option is selected
	if eval (GetUIFloat "StartMenu/MCM/_ShowList") == 1
		Call (CompileScript "MCMExtender\MenuShowList.gek")
	endif

	; ===== SHOW SCALE =====
	; Stent: Runs a scale option is selected
	; Eg. Int, Float, RBG, Multi Value. Type: 2, 2.5, 8, 9
	if eval (GetUIFloat "StartMenu/MCM/_ShowScale") == 1
		Call (CompileScript "MCMExtender\MenuShowScale.gek")
	endif

	; ===== DEFAULT SCALE =====
	; Stent: Runs when the "Default" button is pressed in scale slider menu
	; Eg. Int, Float, RBG, Multi Value. Type: 2, 2.5, 8, 9
	if GetUIFloat "StartMenu/MCM/_DefaultScale"
		Call (CompileScript "MCMExtender\MenuDefaultScale.gek")
	endif

	; Sets option mouseover text
	if eval (iMouseOver = GetUIFloat "StartMenu/MCM/*:1/_optionID")
		if eval Goo1.AuxVarGetFlt "*MCMExt_State" 2 != iMouseOver
			Goo1.AuxVarSetFlt "*MCMExt_State" iMouseOver 2
			SetUIFloat "StartMenu/MCM/MCM_Info/visible" 0
			aJSON = ReadFromJSON (Goo1.AuxVarGetStr "*MCMExt_State" 3) ("/submenus/" + $(GetUIFloat "StartMenu/MCM/_ActiveSubMenu") + "/options/" + $iMouseOver) 0 1
			if eval Ar_HasKey aJSON "mouse"
				if eval (TypeOf aJSON["mouse"]) == "StringMap"
					sTemp = Call (CompileScript "MCMExtender\GetString.gek") (aJSON["mouse"])
				else
					sTemp = aJSON["mouse"]
				endif
				if eval sTemp[0] == "$" && (sTranslate = AuxStringMapGetStr "*MCMExt_Translations" (sTemp)) != ""
					sTemp = sTranslate
				endif
				if eval (sTemp) != ""
					SetUIStringEx "StartMenu/MCM/*:9/string" (sTemp)
					SetUIFloat "StartMenu/MCM/MCM_Info/visible" 1
				endif
			endif
		endif
	elseif eval Goo1.AuxVarGetFlt "*MCMExt_State" 2 > -1
		Goo1.AuxVarSetFlt "*MCMExt_State" -1 2
		SetUIFloat "StartMenu/MCM/MCM_Info/visible" 0
	endif

	; HSV Color Picker Support
	if eval (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/visible") == 1 && (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_UserInput") == 1
		MCMScale.fVTemp1 = GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Red"
		MCMScale.fVTemp2 = GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Green"
		MCMScale.fVTemp3 = GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Blue"
	endif

	; Run scale menu callLoop
	if eval Goo1.AuxVarType "*MCMExt_callLoop" 0 == 1
		if eval GetUIFloat "StartMenu/MCM/_ShowScale" == 3

			iSubmenu = GetUIFloat "StartMenu/MCM/_ActiveSubMenu"
			iOption = GetUIFloat "StartMenu/MCM/_ActiveOption"

			iTemp = GetUIFloat "StartMenu/MCM/*:2/_scales"
			aTemp = ar_Construct "array"
			ar_Append aTemp MCMScale.fVTemp1
			if eval iTemp > 1
				ar_Append aTemp MCMScale.fVTemp2
			endif
			if eval iTemp > 2
				ar_Append aTemp MCMScale.fVTemp3
			endif

			iTemp = Goo1.AuxVarGetFlt "*MCMExt_callLoop" 0
			sTemp = Goo1.AuxVarGetStr "*MCMExt_callLoop" 1
			if eval iTemp == 0
				if TestExpr (rTemp = EditorIDToFormID (sTemp))
					Call (rTemp) iSubmenu iOption aTemp
				endif
			elseif eval iTemp == 1
				Call (CompileScript (sTemp)) iSubmenu iOption aTemp
			elseif eval iTemp == 2
				DispatchEventAlt (sTemp) iSubmenu iOption aTemp
			elseif eval iTemp == 3
				RunScriptSnippet 0 (sTemp)
			elseif eval iTemp == 4
				Console (sTemp)
			endif
		elseif eval GetUIFloat "StartMenu/MCM/_ShowScale" == 0
			Goo1.AuxVarErase "*MCMExt_callLoop"
		endif

	endif

	; Run scale menu callClose
	if eval Goo1.AuxVarType "*MCMExt_callClose" 0 == 1 && GetUIFloat "StartMenu/MCM/_ShowScale" == 0

		iSubmenu = GetUIFloat "StartMenu/MCM/_ActiveSubMenu"
		iOption = GetUIFloat "StartMenu/MCM/_ActiveOption"

		iTemp = Goo1.AuxVarGetFlt "*MCMExt_callClose" 2
		aTemp = ar_Construct "array"
		ar_Append aTemp MCMScale.fVTemp1
		if eval iTemp > 1
			ar_Append aTemp MCMScale.fVTemp2
		endif
		if eval iTemp > 2
			ar_Append aTemp MCMScale.fVTemp3
		endif

		iTemp = Goo1.AuxVarGetFlt "*MCMExt_callClose" 0
		sTemp = Goo1.AuxVarGetStr "*MCMExt_callClose" 1
		if eval iTemp == 0
			if TestExpr (rTemp = EditorIDToFormID (sTemp))
				Call (rTemp) iSubmenu iOption aTemp
			endif
		elseif eval iTemp == 1
			Call (CompileScript (sTemp)) iSubmenu iOption aTemp
		elseif eval iTemp == 2
			DispatchEventAlt (sTemp) iSubmenu iOption aTemp
		elseif eval iTemp == 3
			RunScriptSnippet 0 (sTemp)
		elseif eval iTemp == 4
			Console (sTemp)
		endif

		Goo1.AuxVarErase "*MCMExt_callClose"

	endif

	aJSON = aTemp = Ar_Null
	sv_Destruct sTemp sTranslate sJSONPath

End