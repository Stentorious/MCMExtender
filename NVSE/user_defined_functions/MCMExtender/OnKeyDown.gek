int	iKeyID

int iScale
int iList
int iKey
int iScrollOffset
int iScrollVisible
int iScrollTotal
float xmlCursorPosX
float xmlCursorPosY

begin Function {iKeyID}

	; Ignore inputs
	if eval GetUIFloatAlt "StartMenu/_MCMExt+Open" != 1 || MenuMode 3 || Goo1.AuxVarGetFlt "*MCMExt_State" 7
		return
	endif

	iScale = GetUIFloatAlt "StartMenu/MCM/MCM_Scale/visible"
	iList = GetUIFloatAlt "StartMenu/MCM/MCM_List/visible"
	iKey = GetUIFloatAlt "StartMenu/MCM/MCM_PressControl/visible"

	; Default settings confirmation
	if eval iKeyID == 32768
		if eval GetUIFloatAlt "StartMenu/MCM/MCM_Defaults/target" == 1
			if eval Goo1.AuxVarGetFlt "*MCMExt_Dev" 0
				Goo1.AuxVarSetFlt "*MCMExt_State" 1 7
				MessageBoxExAlt (begin function {int iButton}
					CallAfterFrames 1 ({} => Goo1.AuxVarSetFlt "*MCMExt_State" 0 7) 1
					if iButton == 1
						SetUIFloatAlt "StartMenu/MCM/_Default" 1
					endif
				end) "^Mod Configuration Menu^Reset settings for this page?|No|Yes"
			else
				SetUIFloatAlt "StartMenu/MCM/_Default" 1
			endif
		elseif eval iScale == 1
			SetMCMFloat 0 0 "_DefaultScale" 1
		endif
		return
	endif

	; Exit MCM
	if eval iKeyID == 1
		if eval iKey == 1
			SetMCMFloat 0 0 "MCM_PressControl/visible" 0
			SetMCMFloat 0 0 "MCM_BG/MCM_PressControl/BG/visible" 0
			SetMCMFloat 1 0 "_selected" 0
			SetMCMFloat 0 0 "_NewValue" 1
			SetMCMFloat 0 0 "_Value" 0
		else
			EK 1
			TK 1
		endif
		return
	endif

	; Exit MCM submenu (Tab)
	if eval iKeyID == 15
		if eval iKey == 1
			SetMCMFloat 0 0 "MCM_PressControl/visible" 0
			SetMCMFloat 0 0 "MCM_BG/MCM_PressControl/BG/visible" 0
			SetMCMFloat 1 0 "_selected" 0
			SetMCMFloat 0 0 "_NewValue" 1
			SetMCMFloat 0 0 "_Value" 15
		elseif eval iScale == 1
			ClickMenuTile "StartMenu\MCM\MCM_Scale\Scale_Cancel"
		elseif eval GetUIFloatAlt "StartMenu/MCM/_ShowList" == 3
			SetMCMFloat 0 0 "_ShowList" 0
			SetMCMFloatMass 3 1 10 "_enable" 0
			SetMCMFloat 1 0 "_selected" 0
			SetMCMFloat 3 0 "visible" 0
			MCMList.iTask = 0
			MCMList.iTarget = 0
			SetMCMFloat 0 0 "_TargetList" 0
			StopQuest MCMList
		else
			ClickMenuTile "StartMenu\MCM\MCM_Back"
		endif
		return
	endif

	; Scroll
	if eval iKeyID == 264 || iKeyID == 265
		if eval iKey == 1
			SetMCMFloat 0 0 "MCM_PressControl/visible" 0
			SetMCMFloat 0 0 "MCM_BG/MCM_PressControl/BG/visible" 0
			SetMCMFloat 1 0 "_selected" 0
			SetMCMFloat 0 0 "_NewValue" 1
			SetMCMFloat 0 0 "_Value" iKeyID
			return
		elseif eval iScale == 1
			if eval GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/visible" == 0
				if eval iKeyID == 264
					MCMScale.iTask = 6
				elseif eval iKeyID == 265
					MCMScale.iTask = 5
				endif
			endif
			return
		elseif eval iList == 0
			xmlCursorPosX = (((GetUIFloatAlt "StartMenu\_screen_width") / (Goo1.AuxVarGetFlt "*_ScreenWidth")) * (GetCursorPos X))
			xmlCursorPosY = (((GetUIFloatAlt "StartMenu\_screen_height") / (Goo1.AuxVarGetFlt "*_ScreenHeight")) * (GetCursorPos Y))
			if eval xmlCursorPosX > GetUIFloatAlt "StartMenu\MCM\MCM_Options\x" && xmlCursorPosX < (GetUIFloatAlt "StartMenu\MCM\MCM_Options\x" + GetUIFloatAlt "StartMenu\MCM\MCM_Options\width") && xmlCursorPosY > GetUIFloatAlt "StartMenu\MCM\MCM_Options\y" && xmlCursorPosY < (GetUIFloatAlt "StartMenu\MCM\MCM_Options\y" + GetUIFloatAlt "StartMenu\MCM\MCM_Options\height") && GetUIFloatAlt "StartMenu\MCM\MCM_Options\Scrollbar\visible" == 1
				iScrollOffset = GetUIFloatAlt "StartMenu\MCM\MCM_Options\Scrollbar\_current_value"
				iScrollVisible = GetUIFloatAlt "StartMenu\MCM\MCM_Options\Scrollbar\_number_of_visible_items"
				iScrollTotal = GetUIFloatAlt "StartMenu\MCM\MCM_Options\Scrollbar\_number_of_items"
				if eval iKeyID == 264 && iScrollOffset > 0
					MCMNavigate.iTask = 11
				elseif eval iKeyID == 265 && (iScrollOffset + iScrollVisible) < iScrollTotal
					MCMNavigate.iTask = 12
				endif
			elseif eval xmlCursorPosX > GetUIFloatAlt "StartMenu\MCM\MCM_ModList\x" && xmlCursorPosX < (GetUIFloatAlt "StartMenu\MCM\MCM_ModList\x" + GetUIFloatAlt "StartMenu\MCM\MCM_ModList\width") && xmlCursorPosY > GetUIFloatAlt "StartMenu\MCM\MCM_ModList\y" && xmlCursorPosY < (GetUIFloatAlt "StartMenu\MCM\MCM_ModList\y" + GetUIFloatAlt "StartMenu\MCM\MCM_ModList\height") && GetUIFloatAlt "StartMenu\MCM\MCM_ModList\Scrollbar\visible" == 1
				iScrollOffset = GetUIFloatAlt "StartMenu\MCM\MCM_ModList\Scrollbar\_current_value"
				iScrollVisible = GetUIFloatAlt "StartMenu\MCM\MCM_ModList\Scrollbar\_number_of_visible_items"
				iScrollTotal = GetUIFloatAlt "StartMenu\MCM\MCM_ModList\Scrollbar\_number_of_items"
				if eval iKeyID == 264 && iScrollOffset > 0
					MCMNavigate.iTask = 8
				elseif eval iKeyID == 265 && (iScrollOffset + iScrollVisible) < iScrollTotal
					MCMNavigate.iTask = 9
				endif
			endif
		endif
		return
	endif

	; HSV color picker window mouse click
	if eval iKeyID == 256 && GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/visible" == 1
		if eval GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Window/mouseover" == 1
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragoffsetx" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragoffsety" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragx" ((GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Window/dragoffsetx") - ((GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/width") / 2))
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragy" ((GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Window/dragoffsety") - ((GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/height") / 2))
			DK 256
			CallAfterFrames 3 ({} => EK 256) 1
		elseif eval GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Window/mouseover" == 1
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/dragoffsetx" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/dragoffsety" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/dragy" ((GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Window/dragoffsety") - ((GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/height") / 2))
			DK 256
			CallAfterFrames 3 ({} => EK 256) 1
		endif
		return
	endif

	; Ctrl held
	if eval IsKeyPressed 29 2 == 0 && IsKeyPressed 157 2 == 0
		return
	endif

	; Ctrl + F (Search Bar)
	if eval iKeyID == 33
		if eval GetUIFloatAlt "StartMenu\_MCMExt+Search" != 1
			ClickMenuButton "StartMenu#606"
		else
			if eval GetUIString "StartMenu\MCM\MCM_Search\_search_term" != ""
				SetUIFloatAlt "StartMenu\_MCMExt+Search" 2
			else
				ClickMenuButton "StartMenu#607"
			endif
		endif
		return

	elseif eval GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/visible" == 1

		; Ctrl + C (Copy)
		if eval iKeyID == 46
			Goo1.AuxVarErase "*MCMExt_Copy"
			Goo1.AuxVarSetFlt "*MCMExt_Copy" 1 0
			Goo1.AuxVarSetFlt "*MCMExt_Copy" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Red") 1
			Goo1.AuxVarSetFlt "*MCMExt_Copy" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Green") 2
			Goo1.AuxVarSetFlt "*MCMExt_Copy" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_Blue") 3
			MessageExAlt 3 "#3|MCM Extender: Copy color"
			return

		; Ctrl + V (Paste)
		elseif eval iKeyID == 47
			if eval Goo1.AuxVarGetFlt "*MCMExt_Copy" 0 != 1
				return
			endif

			; Set input traits
			MCMScale.fVTemp1 = Goo1.AuxVarGetFlt "*MCMExt_Copy" 1
			MCMScale.fVTemp2 = Goo1.AuxVarGetFlt "*MCMExt_Copy" 2
			MCMScale.fVTemp3 = Goo1.AuxVarGetFlt "*MCMExt_Copy" 3
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_InputRed" (Goo1.AuxVarGetFlt "*MCMExt_Copy" 1)
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_InputGreen" (Goo1.AuxVarGetFlt "*MCMExt_Copy" 2)
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_InputBlue" (Goo1.AuxVarGetFlt "*MCMExt_Copy" 3)
			; Set picker drag coordinates
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragoffsetx" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragoffsety" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/dragoffsetx" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/dragoffsety" 0
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragx" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_OutputSVDragX")
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/SV_Picker/dragy" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_OutputSVDragY")
			SetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/Hue_Picker/dragy" (GetUIFloatAlt "StartMenu/MCM/MCM_Scale/HSVColorPicker/_OutputHueDragY")
			MessageExAlt 3 "#3|MCM Extender: Paste color"
		endif
	endif

end