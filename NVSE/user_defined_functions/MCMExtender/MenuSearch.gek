string_var sSearch

int iSize
ref rEntry
string_var sName

begin Function {sSearch}

	; Update search term
	Goo1.AuxVarSetFlt "*MCMExt_Search" 1

	; Don't update search if entering previous term
	if eval GetUIString "StartMenu\MCM\MCM_Search\_search_term" == (sSearch)
		sv_Destruct sSearch
		return
	endif

	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_term" (sSearch)

	ListClear MCMMods

	; Add matching forms to list
	iSize = Goo1.AuxVarSize "*MCMListCache"
	while (iSize -= 1) > -1
		rEntry = Goo1.AuxVarGetRef "*MCMListCache" iSize
		if eval (Sv_Find (sSearch) (LNGetName rEntry)) > -1
			ListAddForm MCMMods rEntry
		endif
	loop

	; Validate and add temp clone to MCM FormList
	if eval (iSize = Goo1.AuxVarSize "*MCMExt_ModName") > 0
		while (iSize -= 1) > -1
			sName = Goo1.AuxVarGetStr "*MCMExt_ModName" iSize
			if eval IsFormValid (rEntry = Goo1.AuxVarGetRef "*MCMExt_Clones" iSize) == 0 || GetType rEntry != 31 || GetValueAlt rEntry != iSize || GetName rEntry != (sName)
				rEntry = TempCloneForm "a"
				SetValueAlt rEntry iSize
				SetNameEx (sName) rEntry
				Goo1.AuxVarSetRef "*MCMExt_Clones" rEntry iSize
			endif
			if eval (Sv_Find (sSearch) (sName)) > -1
				ListAddForm MCMMods rEntry
			endif
		loop
	endif

	SetUIFloatAlt "StartMenu\MCM\MCM_Search\_search_count" (ListGetCount MCMMods)

	; Reset MCM
	SetMCMFloat 5 0 "_count" 0
	SetMCMFloat 5 0 "_offset" 0
	SetMCMFloat 0 0 "_ActiveOption" 0
	SetMCMFloat 5 0 "_selected" -1
	SetMCMFloat 0 0 "_ActiveMod" 0
	MCMModSkip = 0
	MCMSubShift = 0
	MCMModList.iModIndex = 0
	MCMModList.iWidthCount2 = 0
	MCMModList.fSubWidth = 0
	MCMModList.iSubCount = 0
	MCMModList.rRef3 = Player
	MCMModsListed.RemoveAllItems
	SetMCMFloat 0 0 "_ActiveSubMenu" 0
	SetMCMFloatMass 5 24 33 "_enable" 0
	SetStage MCM 1

	sv_Destruct sSearch sName

end