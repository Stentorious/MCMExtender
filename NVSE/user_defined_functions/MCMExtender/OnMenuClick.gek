int iMenuID
int iTileID
string_var sTileName

begin Function {iMenuID, iTileID, sTileName}

	sv_Destruct sTileName

	if eval GetUIFloatAlt "StartMenu\MCM\_TargetMain" != 1
		return
	endif

	; Search init
	if eval iTileID == 606 || (iTileID == 607 && GetUIFloatAlt "StartMenu\_MCMExt+Search" == 0)
		DK 18
		SetUIFloatGradual "StartMenu\_MCMExt+Pulse" 1
		SetUIFloatGradual "StartMenu\_MCMExt+Pulse" 1 2.5 1 2
		SetUIFloatAlt "StartMenu\MCM\MCM_Search\_search_count" (ListGetCount MCMMods)
		CallAfterFrames 0 ({} => SetUIFloatAlt "StartMenu\_MCMExt+Search" 1) 1
		return

	; Search reset
	elseif eval iTileID == 607
		EK 18
		Call (CompileScript "MCMExtender\MenuRefresh.gek")
		return

	; Default settings confirmation
	elseif eval iTileID == 609
		if eval Goo1.AuxVarGetFlt "*MCMExt_Dev" 0
			Goo1.AuxVarSetFlt "*MCMExt_State" 1 7
			MessageBoxExAlt (begin function {int iButton}
				CallAfterFrames 1 ({} => Goo1.AuxVarSetFlt "*MCMExt_State" 0 7) 1
				if iButton == 1
					SetUIFloatAlt "StartMenu/MCM/_Default" 1
				endif
			end) "^Mod Configuration Menu^Reset settings for this page?|No|Yes"
		else
			SetUIFloatAlt "StartMenu/MCM/_Default" 1
		endif
		return
	endif

end