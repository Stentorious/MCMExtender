int iMenuID

int iIndex
int iSize
ref rClone
array_var aCache
string_var sName

Begin Function {iMenuID}

	if eval GetUIFloatAlt "StartMenu\_MCMExt+Init"
		return
	endif
	SetUIFloatAlt "StartMenu\_MCMExt+Init" 1

	; Reset message box flag
	Goo1.AuxVarSetFlt "*MCMExt_State" 0 7

	; Init menu loop
	SGMLC (CompileScript "MCMExtender\MenuLoop.gek") 1 1 10

	; Reset search terms
	Goo1.AuxVarSetFlt "*MCMExt_Search" 0
	Goo1.AuxVarSetFlt "*MCMExt_Cursor" 0
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_term" "%e"
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_text_1" "%e"
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_cursor_1" "|"
	SetUIStringAlt "StartMenu\MCM\MCM_Search\_search_cursor_2" " "

	MCMModList.fWidth = 300

	; Get the MCM menu FormList and generate an entry for each mod loaded
	; Remove clones from FormList
	iSize = ListGetCount MCMMods
	while (iSize -= 1) > -1
		rClone = ListGetNthForm MCMMods iSize
		if eval (GetSourceModIndex rClone) == 255
			ListRemoveForm MCMMods rClone
			;PrintC "MCM Extender: Remove clone -> %z" (LNGetName rClone)
		endif
	loop

	; Build search cache, remove extra
	if eval Goo1.AuxVarSize "*MCMListCache" == 0
		aCache = Ar_Unique (GetListForms MCMMods)
		Goo1.AuxVarSetFromArr "*MCMListCache" aCache
	endif

	; Validate and add temp clone to MCM FormList
	if eval (iSize = Goo1.AuxVarSize "*MCMExt_ModName") > 0
		while (iSize -= 1) > -1
			sName = Goo1.AuxVarGetStr "*MCMExt_ModName" iSize
			if eval IsFormValid (rClone = Goo1.AuxVarGetRef "*MCMExt_Clones" iSize) == 0 || GetType rClone != 31 || GetValueAlt rClone != iSize || GetName rClone != (sName)
				rClone = TempCloneForm Caps001
				SetValueAlt rClone iSize
				SetNameEx (sName) rClone
				Goo1.AuxVarSetRef "*MCMExt_Clones" rClone iSize
				;PrintC "MCM Extender: Create clone -> %z" (sName)
			endif
			;PrintC "MCM Extender: Loaded menu -> %n" rClone
			ListAddForm MCMMods rClone
		loop
	endif

	; Set tile visibility
	SetUIFloatAlt "StartMenu\MCM\MCM_Bracket\MCM_Bracket_Bottom\bracket_h1\visible" 0
	SetUIFloatAlt "StartMenu\MCM\MCM_Bracket\MCM_Bracket_Bottom\bracket_v1\visible" 0
	SetUIFloatAlt "StartMenu\MCM\MCM_Bracket\MCM_Bracket_Bottom\bracket_v2\visible" 0
	SetUIFloatAlt "StartMenu\MCM\MCM_Scale\Scale_Accept\text\visible" 0
	SetUIFloatAlt "StartMenu\MCM\MCM_Scale\Scale_Default\text\visible" 0
	SetUIFloatAlt "StartMenu\MCM\MCM_Scale\Scale_Cancel\text\visible" 0

	aCache = Ar_Null
	sv_Destruct sName

End