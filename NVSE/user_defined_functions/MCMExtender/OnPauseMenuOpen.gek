int iMenuID

int iIndex
int iSize
ref rList
ref rClone
string_var sName

Begin Function {iMenuID}

	if eval GetUIFloatAlt "StartMenu\MCM\_Menu" == 1
		return
	endif

	; Get the MCM menu FormList and generate an entry for each mod loaded
	rList = GetFormFromMod "The Mod Configuration Menu.esp" "AE6"

	; Remove clones from FormList
	iSize = ListGetCount rList
	while (iSize -= 1) > -1
		rClone = ListGetNthForm rList iSize
		if eval (GetSourceModIndex rClone) == 255
			ListRemoveForm rList rClone
			;PrintC "MCM Extender: Remove clone -> %z" (LNGetName rClone)
		endif
	loop

	; Validate and add temp clone to MCM FormList
	if eval (iSize = Goo1.AuxVarSize "*MCMExt_ModName") > 0
		while (iSize -= 1) > -1
			sName = Goo1.AuxVarGetStr "*MCMExt_ModName" iSize
			if eval IsFormValid (rClone = Goo1.AuxVarGetRef "*MCMExt_Clones" iSize) == 0 || GetType rClone != 31 || GetValueAlt rClone != iSize || GetName rClone != (sName)
				rClone = TempCloneForm "a"
				SetValueAlt rClone iSize
				SetNameEx (sName) rClone
				Goo1.AuxVarSetRef "*MCMExt_Clones" rClone iSize
				;PrintC "MCM Extender: Create clone -> %z" (sName)
			endif
			ListAddForm rList rClone
		loop
	endif

	sv_Destruct sName

End